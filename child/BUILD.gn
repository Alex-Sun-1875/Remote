# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//ppapi/buildflags/buildflags.gni")
import("//third_party/blink/public/public_features.gni")

if (is_component_build) {
  link_target_type = "source_set"
} else {
  link_target_type = "static_library"
}
target(link_target_type, "child_thread_impl") {
  # Targets external to content should always link to the public API.
  # Internal targets can link to this but only if they're child processes
  # (i.e. not content/browser or content/common) and only if they're inside the
  # content component (i.e. not content/test or content/shell).
  sources = [
    "child_process.cc",
    "child_process.h",
    "child_thread_impl.cc",
    "child_thread_impl.h",
    "thread_safe_sender.cc",
    "thread_safe_sender.h",
  ]

  configs += [ "//build/config:precompiled_headers" ]

  public_deps = [
    "//ipc",
  ]

  deps = [
    ":public_common",
    "//base",
    "//remote/common",
    "//remote/common:mojo_bindings",
    "//content/public/common:service_names",
    "//mojo/core/embedder",
    "//mojo/public/cpp/bindings:bindings",
    "//mojo/public/cpp/platform:platform",
    "//mojo/public/cpp/system:system",
    "//net",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/mojom",
    "//services/service_manager/runner/common",
  ]

  configs += [
    "//build/config/compiler:no_size_t_to_int_warning",
  ]
}

target(link_target_type, "public_common") {
  sources = [
    "//content/public/common/content_switches.h",
    "//content/public/common/content_switches.cc",
    "//content/public/common/service_manager_connection.h",
    "//content/common/service_manager/service_manager_connection_impl.h",
    "//content/common/service_manager/service_manager_connection_impl.cc",
    "//content/public/common/service_names.mojom.h",
    "//content/public/common/simple_connection_filter.h",
    "//content/public/common/simple_connection_filter.cc",
    "//content/public/common/connection_filter.h",
  ]

  public_configs = [ "//v8:external_startup_data" ]

  public_deps = [
    "//remote/common:mojo_bindings",
    "//content/public/common:service_names",
    "//services/service_manager/embedder",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/mojom",
  ]

  deps = []
}
